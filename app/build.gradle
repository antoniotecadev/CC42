plugins {
    id 'com.android.application'
    id 'androidx.navigation.safeargs' // Gerar código de linguagem Java adequado para módulo Java
    id 'com.google.gms.google-services' // Para usar Firebase
}

android {
    namespace 'com.antonioteca.cc42'
    compileSdk 34

    defaultConfig {
        applicationId "com.antonioteca.cc42"
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ''
            }
        }

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            shrinkResources false // Remoção de recursos não utilizados
            minifyEnabled false // Minificação e obfuscação
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        // (zxing-android-embedded) Activar desuring para usar APIs do Java 8+
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding true
        buildConfig = true // A classe BuildConfig é uma classe gerada automaticamente que contém informações relacionadas à construção do projeto, como se você está construindo uma versão de depuração (debug) ou de lançamento (release)
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/INDEX.LIST']
        }
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
// Quando há arquivos duplicados em 'META-INF/DEPENDENCIES' durante a compilação do projecto no Gradle
    // Isso geralmente acontence quando diferentes dependências contêm o mesmo arquivo META INFO

    aaptOptions { // Instrui o Android Build System para: Não comprimir os arquivos .tflite durante a criação do APK.
        noCompress "tflite"
    }
}

dependencies {

    def room_version = "2.6.1"
    def nav_version = "2.5.3"
    def retrofit_version = "2.11.0"
    def glide_version = "4.16.0"
    def camerax_version = "1.4.2"

    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.2.0'
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // Room DataBase
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    // optional - RxJava3 support for Room
    implementation "androidx.room:room-rxjava3:$room_version"

    // RxAndroid
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    // RxJava
    implementation 'io.reactivex.rxjava3:rxjava:3.0.0'

    // Pull to Refresh
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'

    // Navigation Component
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    // Fornece suporte à navegação entre fragmentos (NavHostFragment e NavController)
    implementation "androidx.navigation:navigation-ui:$nav_version"
    // Integração (Toolbar), BottomNavigationView, NavigationView e (Up button)
    implementation "androidx.navigation:navigation-runtime:$nav_version"
    //  Contém o núcleo da API de navegação para permitir a navegação em tempo de execução.

    // Client HTTP for consumer API REST
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    // define a BOM and its version
    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.12.0"))
    // define any required OkHttp artifacts without version
    implementation("com.squareup.okhttp3:okhttp")
    implementation("com.squareup.okhttp3:logging-interceptor")

    // Glide ImageView
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.12.0'
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    // Markdown
    implementation 'io.noties.markwon:core:4.6.2'

    // QR Code
    implementation 'com.journeyapps:zxing-android-embedded:4.3.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Import the BoM for the Firebase platform
    implementation(platform("com.google.firebase:firebase-bom:32.3.1"))

    // Add the dependency for the Realtime Database library
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation("com.google.firebase:firebase-database")

    // Add the dependency for the Messaging library
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-messaging'

    // Add the sependency for the Auth library
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-auth'

    // Gerar Token OAuth
    implementation('com.google.auth:google-auth-library-oauth2-http:1.22.0') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'commons-logging', module: 'commons-logging'
    }

    //itext for genereted pdf
    implementation 'com.itextpdf:itext7-core:7.2.3'

    // Cloudinary for storage Image
    implementation 'com.cloudinary:cloudinary-android:3.0.2'

    // CameraX para captura de imagens
    implementation "androidx.camera:camera-core:$camerax_version"
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    implementation "androidx.camera:camera-view:$camerax_version"

    // ML Kit para detecção facial
    implementation 'com.google.mlkit:face-detection:16.1.7'

    // TensorFlow Lite para reconhecimento facial
    implementation 'org.tensorflow:tensorflow-lite:2.12.0'
    implementation 'org.tensorflow:tensorflow-lite-support:0.4.3'
    implementation 'org.tensorflow:tensorflow-lite-task-vision:0.4.3'
}